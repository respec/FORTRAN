Purpose -   
<p>Flow of Execution
<ul>
<li><a href="#L1"></a>
</ul>
<p>Code Group 
   <a href="../HDATUT">HDATUT</a>
<p>Called By

<p>Common Blocks <i>none</i>
<p>Calls
   COPYI,
   TIMCVT
<p>Code
<pre>
C
C
C
      SUBROUTINE   MJDATE
     I                    (EXDAT,
     O                     JTIME,DYFRAC)
C
C     + + + PURPOSE + + +
C     Convert external format integer date to modified julian date and
C     fraction of the day.
C
C     + + + DUMMY ARGUMENTS + + +
      INTEGER   EXDAT(5)
      DOUBLE PRECISION JTIME,DYFRAC
C
C     + + + ARGUMENT DEFINITIONS + + +
C     EXDAT  - external date format
C     JTIME  - modified julian date and time of day
C     DYFRAC - fraction of day
C
C     + + + LOCAL VARIABLES + + +
      INTEGER I,A,B,Y,M,MJD,XDAT(6)
C
C     + + + EXTERNALS + + +
      EXTERNAL COPYI,TIMCVT
C
C     + + + INTRINSICS + + +
      INTRINSIC INT,DBLE
C
C     + + + END SPECIFICATIONS + + +
C
      I= 5
      CALL COPYI (I,EXDAT,
     O            XDAT)
      XDAT(6)= 0
C     convert to midnight convention of 00:00
      CALL TIMCVT
     M            (XDAT)
C
      IF (XDAT(2) .GT. 2) THEN
C       use actual values for March-December
        Y= XDAT(1)
        M= XDAT(2)
      ELSE
C       use artificial values for Jan-Feb
        Y= XDAT(1)- 1
        M= XDAT(2)+ 12
      END IF
C
C     set up other values
      A= Y/100
      B= 2 - A + A/4
C
C     compute julian date
      MJD= (36525*Y)/100+ INT (30.6001*(M+ 1))+ XDAT(3)+ B- 679006
C
C     convert to double precision
      DYFRAC= DBLE (XDAT(4))/24.D0+ DBLE (XDAT(5))/1440.D0
      JTIME= DBLE (MJD)+ DYFRAC
C
      RETURN
      END
</pre>



















