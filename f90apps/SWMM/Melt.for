      SUBROUTINE MELT(DHM,TBAS,RIN,TA,DELT,SMELT,COLD,ATIX,WINDY,
     1            ASC,SNOWWE,KOMPUT)
C     RUNOFF BLOCK
C     CALLED BY SNOW NEAR LINE 98
C=======================================================================
C     UF SNOW MELT SUBROUTINE. WRITTEN OCT. 4, 1977, BY W. HUBER
C
C     ROUTINE GENERALLY MODELED AFTER SIMILAR ROUTINE IN CANADIAN SWMM.
C     ISNOW=1 - MELT FOR SINGLE EVENT.
C     ISNOW=2 - MELT FOR CONTINUOUS SIMULATION.
C=======================================================================
      INCLUDE 'TAPES.INC'
      INCLUDE 'SUBCAT.INC'
CWRMb save variables between subroutine calls
      SAVE SMSAVE
CWRMe
C
C     INPUT SNOW OR RAIN IS IN FT/SEC.
C     CONVERT TO IN./HR FOR SOME USES.
C
      PREC = RIN*43200.0
      IF(PREC.GT.0.02) GO TO 30
C
C     HERE, USE DEGREE TYPE EQUATION.
C
      IF(TA.LT.TBAS) GO TO 20
C
C     HERE, MELT SNOW. DHM IS IN FT/SEC UNITS.
C     MELT IS A RATE (FT/SEC) PER UNIT SNOW-COVERED AREA.
C     STORAGES ARE IN FT WATER EQUIVALENT OVER TOTAL AREA.
C     HENCE, MUST ADJUST STORAGES BY AREAL DEPLETION FACTOR.
C     ASC=FRACTION OF AREA COVERED BY SNOW.
C
      SMELT = DHM*(TA-TBAS)
   10 IF(ISNOW.NE.2) RETURN
C
C     FOR CONTINUOUS SIMULATION MUST KEEP TRACK OF COLD CONTENT OF SNOW.
C
      IF(COLD.LE.0.0) RETURN
C
C     SUBTRACT COLD CONTENT FROM POTENTIAL MELT.
C     USE NEGATIVE MELT FACTOR (RNM*DHM) WHEN WARMING SNOW.
C
      SMELT1 = SMELT
      SMELT = SMELT1-COLD/DELT/RNM/ASC
      IF(SMELT.LE.0.0) COLD  = COLD-SMELT1*DELT*RNM*ASC
      IF(COLD.LT.0.0)  COLD  = 0.0
      IF(SMELT.GT.0.0) COLD  = 0.0
      IF(SMELT.LT.0.0) SMELT = 0.0
      ATIX = TBAS
      RETURN
C
C     HERE, ALTER COLD CONTENT ACCORDING TO ANTECEDENT TEMPERATURE
C        INDEX AND NEGATIVE MELT FACTOR.
C
   20 IF(ISNOW.NE.2) RETURN
      ATIX = ATIX+TIPM*(TA-ATIX)
C
C     IF SNOWING, LET ATIX ASSUME SNOW TEMPERATURE (AIR TEMPERATURE).
C
      IF(PREC.LT.-0.02) ATIX = TA
      IF(ATIX.GT.TBAS)  ATIX = TBAS
      COLD = COLD+RNM*DHM*(ATIX-TA)*DELT*ASC
      IF(COLD.LT.0.0) COLD = 0.0
C
C     MAXIMUM COLD CONTENT IS BASED ON ASSUMED SPECIFIC HEAT OF SNOW
C        OF 0.007 IN. WATER EQUIVALENT PER DEG. F.
C     THE NUMBER 0.00058333 = 0.007/12.
C
      COLMAX = SNOWWE*0.00058333*(TBAS-ATIX)
      IF(COLD.GT.COLMAX) COLD = COLMAX
      RETURN
C
C     HERE, USE ANDERSON'S US WEATHER SERVICE EQN IF RAINING.
C     WHEN RAINING, USE SAME COMPUTATION FOR ALL SUBCATCHMENTS.
C
   30 IF(KOMPUT.EQ.0) GO TO 40
      SMELT = SMSAVE
      GO TO 10
C
C     USE EXPONENTIAL APPROX. FOR SATURATION VAPOR PRESSURE IN IN.-HG.
C
   40 EA=8.1175E6*EXP(-7701.544/(TA+405.0265))
C
C     WIND HAS BEEN READ AS MPH.
C
      UADJ = 0.006*WINDY
C
C     COMPUTE SNOW MELT IN IN./HR.
C
      SMELT=(TA-32.)*(0.001167+SGAMMA*UADJ+0.007*PREC)+8.5*UADJ*(EA-.18)
C
C     CONVERT TO FT/SEC.
C
      SMELT  = SMELT/43200.0
      SMSAVE = SMELT
      KOMPUT = 1
      GO TO 10
      END
